plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${modname}-${mcversion}-fabric"

repositories {
    mavenCentral()
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url "https://maven.bai.lol" }
    maven { url = 'https://maven.terraformersmc.com/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${mcversion}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabricloaderversion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricversion}"

    implementation project(":shared")

    modImplementation "net.minecraftforge:forgeconfigapiport-fabric:3.2.4"

    modCompileOnly "curse.maven:jade-324717:3922834"
//    modRuntimeOnly "curse.maven:jade-324717:3922834"

    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-4.13.3"
    modRuntimeOnly "mcp.mobius.waila:wthit:fabric-4.13.3"
    modRuntimeOnly "lol.bai:badpackets:fabric-0.1.2"

    modRuntimeOnly "curse.maven:sodium-394468:3669187"
    modRuntimeOnly "curse.maven:starlight-521783:3667443"
//    modRuntimeOnly "curse.maven:terrestria-323974:4166789"

    modCompileOnly "com.terraformersmc.terraform-api:terraform-wood-api-v1:3.1.0"
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", version

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
    source(project(":shared").sourceSets.main.allSource)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present. If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}"}
    }
}

loom {
    runs {
        client {
            client()
            setConfigName("fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}
