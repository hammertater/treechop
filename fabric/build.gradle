plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${modname}-${mcversion}-fabric"

repositories {
    mavenCentral()
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    maven { url "https://maven.bai.lol" }
}

dependencies {
    minecraft "com.mojang:minecraft:${mcversion}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabricloaderversion}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricversion}"
    modImplementation "net.minecraftforge:forgeconfigapiport-fabric:4.2.6"

    implementation project(":shared")

    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-5.9.0"
    modRuntimeOnly "mcp.mobius.waila:wthit:fabric-5.9.0"
    modRuntimeOnly "lol.bai:badpackets:fabric-0.2.0"

    modCompileOnly "curse.maven:jade-324717:3952631"
//    modRuntimeOnly "curse.maven:jade-324717:3952631"

//    modRuntimeOnly "curse.maven:sodium-394468:3957319"
//    modRuntimeOnly "curse.maven:indium-459496:3957480"
//    modRuntimeOnly "curse.maven:irisshaders-455508:3981799"
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", version

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
    source(project(":shared").sourceSets.main.allSource)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present. If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}"}
    }
}

loom {
    runs {
        client {
            client()
            setConfigName("fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        fabricJar(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
}
