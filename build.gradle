plugins {
    id "fabric-loom" version "1.7-SNAPSHOT" apply false
    id "net.minecraftforge.gradle" version "[6.0,6.2)" apply false
    id "org.spongepowered.gradle.vanilla" version "0.2.1-SNAPSHOT" apply false
    id "org.spongepowered.mixin" version "0.7-SNAPSHOT" apply false
}

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))
subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: 'maven-publish'

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)

    version = mod_version
    group = mod_group

    base {
        archivesName = "$mod_name-$minecraft_version-$project.name"
    }

    jar {
        manifest {
            attributes([
                    "Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : "$mod_id-$project.name",
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://www.cursemaven.com" }
        maven { url "file://${rootDir}/publishing" }
        maven { url = "https://api.modrinth.com/maven" }
        maven { url "https://maven.bai.lol" }
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content { includeGroup "org.spongepowered" }
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
        testImplementation group: "org.hamcrest", name: "hamcrest", version: "2.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
        implementation "com.google.code.findbugs:jsr305:3.0.2"
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact jar
            }
        }
        repositories {
            maven {
                url "file://${rootDir}/publishing"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 21
    }
}
