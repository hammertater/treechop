plugins {
    id("org.spongepowered.mixin") version "0.7-SNAPSHOT" apply(false)
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: 'maven-publish'
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    jar {
        manifest {
            attributes([
                    "Specification-Title"     : modid,
                    "Specification-Vendor"    : "hammertater",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : "hammertater",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://www.cursemaven.com" }
        maven { url "file://${rootDir}/publishing" }
        maven { url = "https://api.modrinth.com/maven" }
        maven { url "https://maven.bai.lol" }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact jar
            }
        }
        repositories {
            maven {
                url "file://${rootDir}/publishing"
            }
        }
    }
}
