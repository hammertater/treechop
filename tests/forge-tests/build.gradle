plugins {
    id "net.minecraftforge.gradle" version "[6.0,6.2)"
    id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
}

minecraft {
    mappings channel: "official", version: minecraft_version
    copyIdeResources = true

    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            mods {
                "$mod_id" {
                    source sourceSets.main
                    source project(":tuber").sourceSets.main
                    source project(":shared").sourceSets.main
                }
            }
        }

        client {
            property "forge.enabledGameTestNamespaces", mod_id
        }

        server {
            property "forge.enabledGameTestNamespaces", mod_id
        }

        gameTestServer {
            property "forge.enabledGameTestNamespaces", mod_id
        }

        data {
            args "--mod", mod_id, "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
        }
    }
}

sourceSets.main.resources {
    srcDir "src/generated/resources"
}

mixin {
    add(sourceSets.main, "treechoptests.mixins.refmap.json")
    config "treechoptests.mixins.json"
}

test {
    useJUnitPlatform()
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy("reobfJar")
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn("reobfJar")
